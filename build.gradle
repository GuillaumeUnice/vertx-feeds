plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'com.github.johnrengelman.shadow' version '1.2.1'
}

repositories {
	mavenCentral()
}

def vertxVersion = '3.1.0'

dependencies {
    
    // Vert.x standard
	compile "io.vertx:vertx-core:$vertxVersion"
	compile "io.vertx:vertx-web:$vertxVersion"
    
    // MongoDB
    compile "io.vertx:vertx-mongo-client:$vertxVersion"
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.47.0'
	// Redis
	compile "io.vertx:vertx-redis-client:$vertxVersion"
    compile 'com.github.kstyrc:embedded-redis:0.6'
	
    
    // Handlebars
	compile "io.vertx:vertx-web-templ-handlebars:$vertxVersion"
    compile 'com.github.jknack:handlebars:2.1.0'
    
	// RSS
	compile 'com.rometools:rome:1.5.0'
}

shadowJar  {
    classifier = ''
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'java:io.vertx.examples.feeds.verticles.MainVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
    dependencies {
        exclude(dependency('io.vertx:codegen'))
        exclude(dependency('junit:junit'))
        exclude(dependency('org.mvel:mvel2'))
    }
    into 'webroot', {
        from 'webroot'
    }
    into 'templates', {
        from 'templates'
    }
}

task start(dependsOn: shadowJar) << {
    javaexec { main="-jar"; args shadowJar.archivePath }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}